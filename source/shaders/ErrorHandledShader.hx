package shaders;

import flixel.system.FlxAssets.FlxShader;
import flixel.addons.display.FlxRuntimeShader;
import lime.graphics.opengl.GLProgram;
import lime.app.Application;

class ErrorHandledShader extends FlxShader implements IErrorHandler
{
	public var shaderName:String = '';
	public dynamic function onError(error:Dynamic):Void {}
	public function new(?shaderName:String)
	{
		this.shaderName = shaderName;
		super();
	}

	override function __createGLProgram(vertexSource:String, fragmentSource:String):GLProgram
	{
		try
		{
			final res = super.__createGLProgram(vertexSource, fragmentSource);
			return res;
		}
		catch (error)
		{
			ErrorHandledShader.crashSave(this.shaderName, error, onError);
			return null;
		}
	}
	
	public static function crashSave(shaderName:String, error:Dynamic, onError:Dynamic) // prevent the app from dying immediately
	{
		if(shaderName == null) shaderName = 'unnamed';
		var alertTitle:String = 'Error on Shader: "$shaderName"';

		SlDebug.log(error);

		// Save a crash log on Release builds
		var errMsg:String = "";
		var dateNow:String = Date.now().toString().replace(" ", "_").replace(":", "'");

		if (!FileSystem.exists(#if switch "sdmc:/switch/Switch-Funkin/shaderCrashes/" #else './shaderCrashes/' #end))
			FileSystem.createDirectory(#if switch "sdmc:/switch/Switch-Funkin/shaderCrashes/" #else './shaderCrashes/' #end);

		var crashLogPath:String = #if switch 'sdmc:/switch/Switch-Funkin/shaderCrashes/shader_${shaderName}_${dateNow}.txt' #else './shaderCrashes/shader_${shaderName}_${dateNow}.txt' #end;
		File.saveContent(crashLogPath, error);
		#if !switch
		Application.current.window.alert('Error log saved at: $crashLogPath', alertTitle);
		#else
		SwitchUtils.showError(0, 'Cannot compile GL shader: [$shaderName]\nError log saved at: $crashLogPath');
		#end

		onError(error);
	}
}

class ErrorHandledRuntimeShader extends FlxRuntimeShader implements IErrorHandler
{
	public var shaderName:String = '';
	public dynamic function onError(error:Dynamic):Void {}
	public function new(?shaderName:String, ?fragmentSource:String, ?vertexSource:String)
	{
		this.shaderName = shaderName;
		super(fragmentSource, vertexSource);
	}

	override function __createGLProgram(vertexSource:String, fragmentSource:String):GLProgram
	{
		try
		{
			final res = super.__createGLProgram(vertexSource, fragmentSource);
			return res;
		}
		catch (error)
		{
			ErrorHandledShader.crashSave(this.shaderName, error, onError);
			return null;
		}
	}
}

interface IErrorHandler
{
	public var shaderName:String;
	public dynamic function onError(error:Dynamic):Void;
}